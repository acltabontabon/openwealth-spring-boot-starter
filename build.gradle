plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.16.0'
    id 'org.springframework.boot' version '3.4.4' apply false
    id 'io.spring.dependency-management' version '1.1.7'
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

group = 'com.acltabontabon'
version = '1.0.0-Alpha.3'
description = 'A lightweight and developer-friendly Spring Boot library that simplifies integration with OpenWealth APIs.'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

repositories {
    mavenCentral()
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter'

    implementation 'org.springframework.boot:spring-boot-starter-web'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation platform('org.junit:junit-bom:5.12.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.16.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.16.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }

            pom {
                name = project.name
                description = project.description.toString()
                url = 'https://github.com/acltabontabon/openwealth-spring-starter'
                inceptionYear = '2024'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/license/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'aclt'
                        name = 'Alvin Cris Tabontabon'
                        email = 'me@acltabontabon.com'
                    }
                }
                scm {
                    url = 'https://github.com/acltabontabon/openwealth-spring-starter.git'
                    connection = 'scm:git:git://github.com/acltabontabon/openwealth-spring-starter.git'
                    developerConnection = 'scm:git:git://github.com/acltabontabon/openwealth-spring-starter.git'
                }
                issueManagement {
                    url = 'https://github.com/acltabontabon/openwealth-spring-starter/issues'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        passphrase = System.getenv('JRELEASER_GPG_PASSPHRASE')
        publicKey = System.getenv('JRELEASER_GPG_PUBLIC_KEY')
        secretKey = System.getenv('JRELEASER_GPG_SECRET_KEY')
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }

    upload {
        active = 'always'
    }
}